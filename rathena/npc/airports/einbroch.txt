//===== rAthena Script ======================================= 
//= Einbroch Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

airport,143,43,5	script	Airport Staff#airport1a::airport1	90,{
	mes gettext("[Airport Staff]\n"
		"Welcome to the\n"
		"Einbroch Airport,\n"
		"where we offer nonstop\n"
		"flights to the cities of\n"
		"Juno, Lighthalzen and Hugel.");
	next;
	if (select(gettext("Board the Airship:Cancel")) == 1) {
		mes gettext("[Airport Staff]\n"
			"The Airship boarding fee\n"
			"is 1,200 zeny, but if you've\n"
			"got a Free Ticket for Airship,\n"
			"the fee will be waived. Will\n"
			"you board the Airship?");
		next;
		if (select(gettext("Yes:No")) == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes gettext("[Airport Staff]\n"
				"I'm sorry, but you don't\n"
				"have a Free Ticket for\n"
				"Airship and you don't have\n"
				"enough zeny for boarding\n"
				"the Airship. Remember, the\n"
				"boarding fee is 1,200 zeny.");
			close;
		}
	}
	mes gettext("[Airport Staff]\n"
		"Thank you and\n"
		"have a nice day.");
	close;
}

airport,158,43,5	duplicate(airport1)	Airport Staff#airport1b	90
airport,126,43,5	duplicate(airport1)	Airport Staff#airport1c	90

airport,143,49,3	script	Arrival Staff#airport2a::airport2	90,{
	mes gettext("[Arrival Staff]\n"
		"Welcome to Einbroch Airport.\n"
		"If you are arriving from your\n"
		"flight, let me guide you to the\n"
		"main terminal. Otherwise, please board the Airship to depart to\n"
		"Juno, Lighthalzen and Hugel.");
	next;
	if (select(gettext("Exit to main terminal.:Cancel.")) == 1) {
		mes gettext("[Arrival Staff]\n"
			"Once you're in the main terminal, you will need to pay the fee again\n"
			"to board an Airship. You should\n"
			"only exit if the city of Einbroch\n"
			"is your intended destination.\n"
			"Proceed to the main terminal?");
		next;
		if (select(gettext("Yes:No")) == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes gettext("[Arrival Staff]\n"
		"Alright, thank you\n"
		"for your patronage\n"
		"and I hope you have\n"
		"a pleasant flight~");
	close;
}

airport,126,51,3	duplicate(airport2)	Arrival Staff#airport2b	90
airport,158,50,3	duplicate(airport2)	Arrival Staff#airport2c	90

einbroch,94,267,3	script	Airship Staff#ein01	91,{
	mes gettext("[Airship Staff]\n"
		"Welcome to the\n"
		"Einbroch Airport.\n"
		"Please use this door to\n"
		"board the Airship which stops\n"
		"over Juno, Lighthalzen and\n"
		"Hugel in the Schwaltzvalt Republic.");
	next;
	mes gettext("[Airship Staff]\n"
		"Otherwise, if Einbroch is\n"
		"your intended destination,\n"
		"please head down the stairs\n"
		"and ask the Arrival Staff to lead\n"
		"you to the main terminal. Thank\n"
		"you, and enjoy your travels.");
	close;
}
