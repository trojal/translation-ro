//===== rAthena Script ======================================= 
//= Lighthalzen Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Lighthalzen Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

lhz_airport,143,43,5	script	Airport Staff#lhz_air1a::lhz_airport1	90,{
	mes gettext("[Airport Staff]\n"
		"Welcome to the\n"
		"Lighthalzen Airport,\n"
		"where we offer nonstop\n"
		"flights to Einbroch, Juno and Hugel.");
	next;
	if (select(gettext("Board the Airship.:Cancel.")) == 1) {
		mes gettext("[Airport Staff]\n"
			"The boarding fee is\n"
			"1,200 zeny, but you can\n"
			"waive the fee if you redeem\n"
			"a Free Ticket for Airship.");
		next;
		if (select(gettext("Yes:No")) == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "lhz_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "lhz_airport",148,51;
				end;
			}
			mes gettext("[Airship Staff]\n"
				"I'm sorry, but you don't\n"
				"have 1,200 zeny to pay\n"
				"for the boarding fee.");
			close;
		}
	}
	mes gettext("[Airport Staff]\n"
		"Thank you and\n"
		"please come again.\n"
		"Have a good day~");
	close;
}

lhz_airport,158,43,5	duplicate(lhz_airport1)	Airship Staff#lhz_air1b	90
lhz_airport,126,43,5	duplicate(lhz_airport1)	Airship Staff#lhz_air1c	90

lhz_airport,143,49,3	script	Arrival Staff#lhz_air2a::lhz_airport2	90,{
	mes gettext("[Arrival Staff]\n"
		"Welcome to Lighthalzen Airport.\n"
		"Please let me guide you to the\n"
		"main terminal if you are arriving from your flight. Otherwise, please\n"
		"board the departing Airship to reach your intended destination.");
	next;
	if (select(gettext("Exit to main terminal.:Cancel.")) == 1) {
		mes gettext("[Arrival Staff]\n"
			"Once you're in the main terminal, you will need to pay the fee again\n"
			"to board an Airship. You should\n"
			"only exit if Lighthalzen is your intended destination. Shall we\n"
			"proceed to the main terminal?");
		next;
		if (select(gettext("Yes:No")) == 1) {
			warp "lhz_airport",142,40;
			end;
		}
	}
	mes gettext("[Arrival Staff]\n"
		"Alright, thank you\n"
		"for your patronage\n"
		"and I hope you have\n"
		"a pleasant flight~");
	close;
}

lhz_airport,126,51,3	duplicate(lhz_airport2)	Arrival Staff#lhz_air2b	90
lhz_airport,158,50,3	duplicate(lhz_airport2)	Arrival Staff#lhz_air2c	90
