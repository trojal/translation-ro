//===== rAthena Script ======================================= 
//= Lighthalzen Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Lighthalzen Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

lhz_airport,143,43,5	script	Airport Staff#lhz_air1a::lhz_airport1	90,{
	mes gettext("[Airport Staff]");
	mes gettext("Welcome to the Lighthalzen Airport, where we offer nonstop flights to Einbroch, Juno and Hugel.");
	next;
	if (select(gettext("Board the Airship.") + ":" + gettext("Cancel.")) == 1) {
		mes gettext("[Airport Staff]");
		mes ngettext("The boarding fee is %d zeny, but you can waive the fee if you redeem a Free Ticket for Airship.", 1200);
		next;
		if (select(gettext("Yes") + ":" + gettext("No")) == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "lhz_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "lhz_airport",148,51;
				end;
			}
			mes gettext("[Airship Staff]");
			mes ngettext("I'm sorry, but you don't have %d zeny to pay for the boarding fee.", 1200);
			close;
		}
	}
	mes gettext("[Airport Staff]");
	mes gettext("Thank you and please come again.");
	mes gettext("Have a good day~");
	close;
}

lhz_airport,158,43,5	duplicate(lhz_airport1)	Airship Staff#lhz_air1b	90
lhz_airport,126,43,5	duplicate(lhz_airport1)	Airship Staff#lhz_air1c	90

lhz_airport,143,49,3	script	Arrival Staff#lhz_air2a::lhz_airport2	90,{
	mes gettext("[Arrival Staff]");
	mes gettext("Welcome to Lighthalzen Airport.");
	mes gettext("Please let me guide you to the main terminal if you are arriving from your flight. Otherwise, please board the departing Airship to reach your intended destination.");
	next;
	if (select(gettext("Exit to main terminal.") + ":" + gettext("Cancel.")) == 1) {
		mes gettext("[Arrival Staff]");
		mes gettext("Once you're in the main terminal, you will need to pay the fee again to board an Airship. You should only exit if Lighthalzen is your intended destination. Shall we proceed to the main terminal?");
		next;
		if (select(gettext("Yes") + ":" + gettext("No")) == 1) {
			warp "lhz_airport",142,40;
			end;
		}
	}
	mes gettext("[Arrival Staff]");
	mes gettext("Alright, thank you for your patronage and I hope you have a pleasant flight~");
	close;
}

lhz_airport,126,51,3	duplicate(lhz_airport2)	Arrival Staff#lhz_air2b	90
lhz_airport,158,50,3	duplicate(lhz_airport2)	Arrival Staff#lhz_air2c	90
